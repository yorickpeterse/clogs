import std.cmp.Equal
import std.iter.Stream

impl Array if T: Equal[T] {
  fn pub index_of(value: ref T) -> Option[Int] {
    let mut idx = 0

    while idx < @size {
      if get(idx) == value { return Option.Some(idx) }
      idx += 1
    }

    Option.None
  }
}

impl String {
  fn pub replace(string: String, with: String) -> String {
    let idx = match byte_index(string, starting_at: 0) {
      case Some(v) -> v
      case _ -> return self
    }

    let new = slice(start: 0, size: idx)

    new.append(with.to_byte_array)
    new.append(slice(start: idx + string.size, size: size))
    new.into_string
  }
}

impl Bool {
  fn pub then[T](func: fn -> T) -> Option[T] {
    if self { Option.Some(func.call) } else { Option.None }
  }
}

# https://github.com/inko-lang/inko/issues/638
impl Stream {
  fn pub mut try_each[E](func: fn (T) -> Result[Nil, E]) -> Result[Nil, E] {
    loop {
      match self.next {
        case Some(v) -> try func.call(v)
        case _ -> return Result.Ok(nil)
      }
    }
  }

  fn pub mut try_reduce[A, E](
    accumulator: A,
    func: fn (A, T) -> Result[A, E],
  ) -> Result[A, E] {
    let mut result = accumulator

    loop {
      match self.next {
        case Some(v) -> result = try func.call(result, v)
        case _ -> return Result.Ok(result)
      }
    }
  }
}
